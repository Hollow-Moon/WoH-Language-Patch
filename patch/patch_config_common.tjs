@if(__PATCH_INITIALIZED__==0)
@set(__PATCH_INITIALIZED__=1)

@if(GAME_FATE==0 && GAME_FHAT==0 && GAME_WOHN==0)
@set(GAME_FATE=1)
global.GameID = "FATE";
@endif

@if(GAME_FATE)
global.PatchVersion = [1, 1, 0];
global.PatchDescription = ("Ultimate Patch v%i.%i.%i").sprintf(PatchVersion[0], PatchVersion[1], PatchVersion[2]);
global.PatchInfoURL = "https://hollow-moon.github.io/fate/";
global.PatchSupportURL = "https://hollow-moon.github.io/fate/support/";
@endif

@if(GAME_FHAT)
global.PatchVersion = [1, 0, 2];
global.PatchDescription = ("Voice Patch v%i.%i.%i").sprintf(PatchVersion[0], PatchVersion[1], PatchVersion[2]);
global.PatchInfoURL = "https://hollow-moon.github.io/fhat/";
global.PatchSupportURL = "https://hollow-moon.github.io/fhat/support/";
@endif

@if(GAME_WOHN)
global.PatchVersion = [0, 0, 1];
global.PatchDescription = ("Language Patch v%i.%i.%i").sprintf(PatchVersion[0], PatchVersion[1], PatchVersion[2]);
global.PatchInfoURL = "https://hollow-moon.github.io/wohn/";
global.PatchSupportURL = "https://hollow-moon.github.io/wohn/support/";
@endif

if ((typeof(global.PatchVersion) !== "Object") || (typeof(global.PatchDescription) !== "String") || (typeof(global.PatchInfoURL) !== "String"))
{
	throw new Exception("Patch was not initialized in a correct manner.");
}

System.exceptionHandler = function(e)
{
	Debug.logAsError();

	if (typeof(global.kag) === "Object" && isvalid(global.kag) && typeof(global.kag.fullScreened) === "Integer" && global.kag.fullScreened && typeof(global.kag.onWindowedMenuItemClick) === "Object" && isvalid(global.kag.onWindowedMenuItemClick))
	{
		global.kag.onWindowedMenuItemClick();
	}

	var exception_message = "Unknown error";
	if (typeof(e) === "Object" && isvalid(e))
	{
		if (typeof(e.message) == "String")
		{
			exception_message = e.message;
		}
	}
	Debug.message("Exception: " + exception_message);
	var trace_string = Scripts.getTraceString();
	if (trace_string !== "")
	{
		Debug.message("Trace: " + trace_string);
	}
	var msg_components = [];
	msg_components.add("\"krkr.console.log\" and \"hwexcept.log\" have been created in either the game's or savedata directory.");
	msg_components.add("Please contact the support forum thread with the save data that caused the error, along with the above files and an explanation of when and how it occurred.");
	msg_components.add(("Support forum thread: %s").sprintf(global.PatchInfoURL));
	msg_components.add("");
	msg_components.add("The following script error occurred early in initialization.");
	msg_components.add("--------------------------------------------------------------------------------");
	msg_components.add(exception_message);
	msg_components.add("--------------------------------------------------------------------------------");
	var msg = msg_components.join("\n");
	System.inform(msg);

@if(!KIRIKIROID)
	if ((typeof(global.devMode) === "Integer" ? !global.devMode : true) && (typeof(global.devMode2) === "Integer" ? !global.devMode2 : true))
	{
		if (System.getArgument('-openpatchinfourlonerror') !== "no" && typeof(global.PatchInfoURL) === "String")
		{
			System.shellExecute(global.PatchInfoURL);
		}
	}

	if (typeof(global.kag) === "Object" && isvalid(global.kag) && typeof(global.kag.visible) == "Integer" && global.kag.visible == false)
	{
		System.exit(127);
	}
@endif

	return true;
};

// Prevent premature exit after startup script execution but before main window is created
global.System.exitOnNoWindowStartup = false;

// Ensure patch version gets placed in the log
Debug.notice(("Patch Description: %s").sprintf(global.PatchDescription));

function dmt(msg*) { Debug.message(msg.join(", ")+": "+Scripts.getTraceString()); }

// Default settings
//プラグインへのイベント配信時にエラー停止しないよう暫定的にトラップさせる=1 / させない=0
@set(__PLUGIN_EVENT_ERROR_TRAP__=0)
@set(NEWSTAFFROLLPLUGIN=1)
@set(NEWER4WINDOWMODE=1)
@set(SCENEMENU=0)
@set(ED_BUTTONS=1)
@set(BGM_SELECTION=1)
@set(FADE_BGM_WITH_VOICE=1)
@set(COMMUNE=1)
@set(MOBILE_MODE=1)
@set(HISTORY_CURRENT_PAGE=0)
@set(SAVE_FONT_TAG=0)
@set(HANAFUDA_TRACKGIRLS=1)
@set(HANAFUDA_SCENEMENU=0)
@set(HANAFUDA_STORE_FULL_VOICE_IN_HISTORY=0)
@set(SONY_ATRAC=0)
@set(LE_LOOP=1)
@set(DRAWTEXT_ESCAPE=1)
@set(USE_BYTECODE=0)
@set(SMOOTH_ANIMATIONS=1)
@set(YESNOLAYER=1)
@set(YESNOLAYER_RELATIVE_TO_CURSOR=0)
@set(FREETYPE_FONT=0)
@set(PRERENDERED_FONTS=0)
@set(CHECK_INTERVAL=0)
@set(USE_WUFFMPEG=1)
@set(SHOW_MARGINS=0)
@set(WORDWRAP_BOLDITALIC_MODIFIER=0)
@set(RELOAD_SCENARIO_ON_FONT_CHANGE=1)
@set(USE_KRMEMPLUGIN=1)
@set(USE_MENU_LAYER=0)
@set(ASYNC_IMAGE_LOAD=0)
@set(USE_KR_VARIOUS_PLUGINS=1)
@set(NO_ASYNC_CACHE_IMAGE=1)
@set(HD_COMPATIBLE_SAVES=1)
// history buttons are unused anyway...
@set(HISTORY_BUTTONS=0)
@set(SCALE_ROUND_TOWARDS_NEAREST=0)
@set(PROCESS_WORD_WRAPPING_IN_CHUNKS=1)
@set(MESSAGELAYER_USECHARLAYER=0)
@set(USE_LAYER_IMAGEFUNCTION_REPLACEMENTS=1)
@set(USE_TVPGL_FUNCTION_REPLACEMENTS=1)
@set(USE_GPU_LAYER=0)
@set(USE_KRMPV=1)
@set(USE_LOG_STORAGE_USED=0)

@if(GAME_FATE||GAME_FHAT)
@set(TEXT_DRAWER=1)
@endif

@if(__SELECT_TEXTWINDOW__!=2)
@set(FONT_MENU_OPACITY=1)
@endif

@if(!GAME_WOHN)
@set(ENABLE_LINKS=1)
@endif

@if(kirikiriz&&!GAME_WOHN)
@set(HD_MODE=1)
@endif

@if(kirikiriz||GAME_WOHN)
@set(ZOOM_WINDOW=1)
@set(ZOOM_WINDOW_ENABLE_ONRESIZE=1)
@endif

@if(!kirikiriz)
@set(USE_LAYER_IMAGEFUNCTION_REPLACEMENTS=0)
@set(USE_TVPGL_FUNCTION_REPLACEMENTS=0)
@endif

@if(GAME_WOHN)
@set(WORDWRAP_BOLDITALIC_MODIFIER=0)
@set(CACHE_CHAR_LAYERS_FROM_TEXT=1)
@set(CACHE_CHAR_LAYERS_FROM_TEXT_FIRSTLOAD_ONLY=0)
@set(FREETYPE_FONT=1)
@set(MESSAGELAYER_USECHARLAYER=1)
@set(MESSAGELAYER_ACTIONMANAGER=1)
@set(USE_KRMPV=0)
@endif

{
	if (typeof(global.KirikiriSDL2Internal) === "Object")
	{
		Scripts.eval("@set(ZOOM_WINDOW=0)");
		Scripts.eval("@set(USE_MENU_LAYER=1)");
	}
}

@if(GAME_FATE||GAME_FHAT)
@set(PATCH_RUBY=1)
@endif

// Wide mode should be independent of hd mode, but this is currently not the case.
@if(HD_MODE)
@set(WIDE_MODE=1)
@if(GAME_FATE||GAME_FHAT)
@set(SCALE_USING_FLOATING_POINT=1)
@endif
@if(GAME_WOHN)
@set(SCALE_USING_FLOATING_POINT=0)
@endif
@endif
@set(CROP_MODE=0)
@set(EDGE_BLUR_HQ=1)

{
	var basepath = "";
	var arcdelim = System.getArgument("-arcdelim");
	if (!arcdelim)
	{
		arcdelim = ">";
	}
	var currentpath = Storages.getFullPath("./");
	if (currentpath.indexOf(arcdelim) === -1)
	{
		basepath = Storages.getFullPath("../");
	}
	else
	{
		basepath = System.exePath;
	}
	global.devMode = ((("" + System.getArgument('-devmode')).indexOf("1") !== -1) || Storages.isExistentStorage(basepath + "dev.txt"));
	global.devMode2 = ((("" + System.getArgument('-devmode')).indexOf("2") !== -1) || Storages.isExistentStorage(basepath + "dev2.txt"));
	global.forceLog = ((System.getArgument('-forcelog') !== void) || Storages.isExistentStorage(basepath + "forceLog.txt"));
	global.useUnencryptedXP3Archive = ((System.getArgument('-useunencryptedxp3archive') !== void) || Storages.isExistentStorage(basepath + "useunencryptedxp3archive.txt"));

	var version_info_path = "patch_versioninfo.tjs";
	try
	{
		if (Storages.isExistentStorage(version_info_path))
		{
			var verinfo = Scripts.execStorage(version_info_path);
			// Ensure patch info gets put in the log
			Debug.notice(("Patch Commit: %s").sprintf(verinfo.hexsha));
			Debug.notice(("Patch Creation Time: %i").sprintf(verinfo.archive_created_time));
		}
	}
	catch (e)
	{
		// pass
	}

	// Override settings
	var settingsPath = basepath + "settings.tjs";
	if (Storages.isExistentStorage(settingsPath))
	{
		var _ = Scripts.evalStorage(settingsPath);
	}

	global.typemoon_repo_root = void;
	if (global.devMode)
	{
		global.typemoon_repo_root = Storages.getFullPath(basepath + "/../");
		Storages.addAutoPath(global.typemoon_repo_root + "common/data/");
		Storages.addAutoPath(global.typemoon_repo_root + "common/patch/");
		Storages.addAutoPath(global.typemoon_repo_root + "common/plugin/");
		Storages.addAutoPath(global.typemoon_repo_root + "common/pluginpatch/");
		Storages.addAutoPath(basepath + "/plugin/");
		Storages.addAutoPath(basepath + "/pluginpatch/");
	}
}

{
	global.plugin_postfix = "";
	if (global.System.platformName.substring(0, 5) === "Win64" && global.System.exeBits === 64)
	{
		global.plugin_postfix = "_intel64";
	}
	global.get_full_path_of_plugin = function(storage)
	{
		if (storage.indexOf(":") === -1)
		{
			var plugin_name_mutated = storage;
			plugin_name_mutated = plugin_name_mutated.substring(0, plugin_name_mutated.length - 4) + global.plugin_postfix + plugin_name_mutated.substring(plugin_name_mutated.length - 4, 4);
			var plugin_paths = [
				plugin_name_mutated,
@if(GAME_WOHN)
				global.System.exePath + "content-data/plugin/" + plugin_name_mutated,
@endif
			];
			for (var i = 0, internal_forloop_count = plugin_paths.count; i < internal_forloop_count; i += 1)
			{
				var path = "" + plugin_paths[i];
				if (path !== "" && global.Storages.isExistentStorage(path))
				{
					return path;
				}
			}
		}
		return "";
	};
	global.Plugins.link_patch_storages_original = global.Plugins.link;
	global.Plugins.link = function(storage)
	{
		var storage_cur = storage;
		var fullpath = global.get_full_path_of_plugin(storage);
		if (fullpath !== "")
		{
			storage_cur = fullpath;
		}
		return global.Plugins.link_patch_storages_original(storage_cur);
	};
}

@if(GAME_FHAT)
var mainPatchName = devMode? "data" : "3";
@endif
@if(GAME_FATE||GAME_WOHN)
var mainPatchName = devMode? "data" : "";
@endif

if (forceLog)
{
	Debug.startLogToFile(false);
}

KAGLoadScript("patch_initialize.tjs");

@endif
