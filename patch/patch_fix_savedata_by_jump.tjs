
global.KAGWindow_patch_fix_savedata_by_jump_original = KAGWindow;
class KAGWindow_patch_fix_savedata_by_jump_override extends KAGWindow_patch_fix_savedata_by_jump_original
{
	function KAGWindow_patch_fix_savedata_by_jump_override()
	{
		super.KAGWindow(...);
	}

	function KAGWindow()
	{
		KAGWindow_patch_fix_savedata_by_jump_override(...);
	}

	function internalStoreFlags(f)
	{
		super.internalStoreFlags(...);

		f.patch_version = global.PatchVersion;
	}

	function restoreFlags()
	{
		if (this.sflags.savedataRepair || (pcflags.patch_version === void || (pcflags.patch_version.join("|") !== global.PatchVersion.join("|"))))
		{
			(Dictionary.assignStruct incontextof flags)(pflags);
			
			var save_testmode = global.testmode;
			global.testmode = true;
			var scenario = pcflags.mainConductor.storageShortName;
			var flowfile = void;
			var flowid = void;
@if(GAME_FATE||GAME_FHAT)
			if (typeof(pcflags.current_id) === "Integer" && typeof(pcflags.current_flowfile) === "String")
			{
				scenario = void;
				flowid = pcflags.current_id;
				flowfile = pcflags.current_flowfile;
			}
@endif
@if(GAME_WOHN)
			var wanted_scriptno = pcflags.scriptno - 1;
			if (typeof(pcflags.scriptno) === "Integer" && typeof(pcflags.scripts) === "Object" && pcflags.scripts.count > wanted_scriptno)
			{
				scenario = pcflags.scripts[wanted_scriptno];
			}
			if (typeof(pflags.av_no) === "Integer" && typeof(pflags.av_storage) === "Object" && pflags.av_storage.count > pflags.av_no)
			{
				global.startArchiveWithScenario(pflags.av_storage[pflags.av_no]);
			}
			else
@endif
			{
				global.open_scenario_using_scenario_tracker(this, scenario, flowid, flowfile);
			}
			this.set_skip(, pcflags.mainConductor.curLabel);
@if(!GAME_WOHN)
			this.do_immediate_skip();
@endif
			global.testmode = save_testmode;
			return;
		}
		return super.restoreFlags();
	}

}
global.KAGWindow = global.KAGWindow_patch_fix_savedata_by_jump_override;
