
global.Conductor_patch_skip_original = Conductor;
class Conductor_patch_skip_override extends Conductor_patch_skip_original
{
	function Conductor_patch_skip_override()
	{
		super.Conductor(...);
	}

	function Conductor()
	{
		Conductor_patch_skip_override(...);
	}

	function onTag(elm)
	{
		if (elm.exp !== "timeline_object.loadByData(global.__tmp_tla,global.__tla_name)")
		{
			if (typeof(owner.skipToScenario) === "String")
			{
				if (owner.skipToScenario === this.curStorage)
				{
					owner.skipToScenario = void;
				}
			}
			else if (typeof(owner.skipToLabel) === "String")
			{
				if (owner.skipToLabel === this.curLabel)
				{
					owner.skipToLabel = void;
				}
			}
			else if (typeof(owner.skipToLine) === "Integer")
			{
				if (owner.skipToLine < this.curLine)
				{
					owner.skipToLine = void;
				}
			}
			else
			{
				if (typeof(owner.isSkippingToTarget) === "Integer")
				{
					owner.isSkippingToTarget = false;
				}
			}
		}
		if (typeof(owner.isSkippingToTarget) === "Integer")
		{
			if (owner.isSkippingToTarget)
			{
				if (owner.skipMode !== 3)
				{
					owner.skipToStop();
				}
			}
			else
			{
				owner.isSkippingToTarget = void;
				owner.cancelSkip();
			}
		}
		return super.onTag(...);
	}
}
global.Conductor = Conductor_patch_skip_override;

global.KAGWindow_patch_skip_original = KAGWindow;
class KAGWindow_patch_skip_override extends KAGWindow_patch_skip_original
{
	var jumpToLabel;
	var jumpToScenario;
	var skipToLabel;
	var skipToScenario;
	var skipToLine;
	var isSkippingToTarget;
	var shouldSkipImmediate;
	var __patch_skip_shutting_down;

	property visible
	{
		getter
		{
			return super.visible;
		}
		setter (v)
		{
			if (typeof(this.__patch_skip_shutting_down) === "Integer")
			{
				return;
			}
			super.visible = v;
		}
	}

	function KAGWindow_patch_skip_override(ismain, width, height)
	{
		super.KAGWindow(...);
		if (isMain)
		{
			this.jumpToLabel = System.getArgument('-jumplabel');
			this.jumpToScenario = System.getArgument('-jumpscenario');
			this.shouldSkipImmediate = System.getArgument('-skipimmediate');
		}
	}

	function KAGWindow()
	{
		KAGWindow_patch_skip_override(...);
	}

	function set_skip(skipscenario, skiplabel, skipline)
	{
		if (typeof(skipscenario) === "String")
		{
			this.isSkippingToTarget = true;
			this.skipToScenario = skipscenario;
		}
		else
		{
			this.skipToScenario = void;
		}
		if (typeof(skiplabel) === "String")
		{
			this.isSkippingToTarget = true;
			this.skipToLabel = skiplabel;
		}
		else
		{
			this.skipToLabel = void;
		}
		if (typeof(skipline) === "Integer" || typeof(skipline) === "String")
		{
			this.isSkippingToTarget = true;
			this.skipToLine = (skipline | 0) - 2;
		}
		else
		{
			this.skipToLine = void;
		}
		if (typeof(this.skipToScenario) !== "String" && typeof(this.skipToLabel) !== "String" && typeof(this.skipToLine) !== "Integer")
		{
			this.isSkippingToTarget = void;
		}
	}

	function do_immediate_skip()
	{
		while (this.isSkippingToTarget)
		{
			this.conductor.run(true);
			if (typeof(this.actmgr) === "Object")
			{
				this.actmgr.update(System.getTickCount());
			}
		}
	}

	function shutdown()
	{
		this.isSkippingToTarget = false;
		this.__patch_skip_shutting_down = true;
		return super.shutdown(...);
	}

	function process(storage)
	{
		if ((typeof(this.jumpToScenario) === "String" || System.getArgument('-archiveopenscenario') !== void) && storage === "first.ks" && isMain)
		{
			this.set_skip(System.getArgument('-skipscenario'), System.getArgument('-skiplabel'), System.getArgument('-skipline'));
			var ret = super.process((typeof(this.jumpToScenario) === "String") ? this.jumpToScenario : storage, (typeof(this.jumpToLabel) === "String") ? this.jumpToLabel : void, true, typeof(this.shouldSkipImmediate) === "String");
			if (typeof(this.shouldSkipImmediate) === "String")
			{
				this.do_immediate_skip();
			}
			this.jumpToScenario = void;
			this.jumpToLabel = void;
			this.shouldSkipImmediate = void;
			return ret;
		}
		return super.process(...);
	}
}
global.KAGWindow = KAGWindow_patch_skip_override;
