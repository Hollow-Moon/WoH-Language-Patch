
@if(kirikiriz)
global.versionTmp = global.System.versionString.split(".");
global.isNewMovieKrkrz = global.versionTmp.count >=2 && global.versionTmp[0] >=1 && global.versionTmp[1] >=4;
delete global.versionTmp;
@endif

global.Movie_patch_movie_override_original = global.Movie;
class Movie_patch_movie_override_override extends global.Movie_patch_movie_override_original
{
	var isWide = false; // Cannot be a property as this causes issues when stopping a movie in full screen in the movie menu.
	var haskrdslavf = false;

	function Movie_patch_movie_override_override()
	{
		super.Movie(...);

		var videosubsystem = global.System.getArgument("-videosubsystem");
		if (!this.haskrdslavf)
		{
			if ((videosubsystem === void) || (videosubsystem == "krdslavf"))
			{
				try
				{
					global.Plugins.link("krdslavf.dll");
					this.haskrdslavf = true;
				}
				catch(e)
				{
					this.haskrdslavf = false;
					if (videosubsystem == "krdslavf")
					{
						throw e;
					}
				}
			}
		}
	}

	function Movie()
	{
		this.Movie_patch_movie_override_override(...);
	}

	function finalize()
	{
		if (this.haskrdslavf)
		{
			if (this.lastStatus == "play")
			{
				this.stop();
			}
			global.Plugins.unlink("krdslavf.dll");
			this.haskrdslavf = false;
		}
		super.finalize(...);
	}

	function onStatusChanged(status)
	{
@if(kirikiriz)
		if (global.isNewMovieKrkrz && this.mode == global.vomMFEVR && this.status == "ready")
		{
			this._onOpen();
		}
		else if (this.lastStatus == "ready" && this.status == "play")
		{
			this.lastStatus = status; // TODO kinda duplicate, but the assignment is needed before the callback.
			this.onPlay();
		}
		else
@endif
		{
			super.onStatusChanged(status);
		}

		this.lastStatus = status;
	}

	function open(storage)
	{
		if (storage == "")
		{
			return false;
		}
		this.owner.saveSystemVariables();
		this.storageName = storage;

@if(kirikiriz)
		if (global.isNewMovieKrkrz)
		{
			if (typeof(this.mp) === "Object")
			{
				this.mode = global.vomMFEVR;
			}
			else
			{
				if(/\.mp4$/i.test(storage))
				{
					if (this.mode != global.vomMFEVR)
					{
						this.mode = global.vomMFEVR;
					}
				}
				else
				{
					this.mode = global.vomOverlay;
				}
				if (this.haskrdslavf)
				{
					this.mode = global.vomOverlay;
				}
			}

		}
		var videosubsystem = this.System.getArgument("-videosubsystem");
		if (videosubsystem) {
			if (videosubsystem == "mediafoundation") {
				this.mode = global.vomMFEVR;
			}
			else if (videosubsystem == "overlay") {
				this.mode = global.vomOverlay;
			}
			else if (videosubsystem == "layer") {
				this.mode = global.vomLayer;
			}
			else if (videosubsystem == "mixer") {
				this.mode = global.vomMixer;
			}
		}
@endif

		// open オーバーライド
		try
		{
			super.open(storage);
@if(kirikiriz)
			if (!global.isNewMovieKrkrz || this.mode != global.vomMFEVR)
@endif
			{
				this._onOpen();
			}

		}
		catch(e)
		{
			if (typeof(e) !== "Object" || !isvalid(e) || typeof(e.message) !== "String")
			{
				return false;
			}
			if (typeof(e.message) === "String")
			{
				global.Debug.message("Movie error message: " + e.message);
			}
			if (typeof(e.trace) === "String")
			{
				global.Debug.message("Movie error message: " + e.trace);
			}
			return false;
		}
		return true;
	}

	function _onOpen()
	{
		this.opened = true;
		this.isWide = (this.originalWidth / this.originalHeight) > 1.7;
		this.onOpen();
	}

	function onOpen()
	{
		// can override.
	}

	function onPlay()
	{
		// can override.
	}
}
global.Movie = global.Movie_patch_movie_override_override;
