
global.KAGWindow_patch_fix_scflags_bookmark_info_original = KAGWindow;
class KAGWindow_patch_fix_scflags_bookmark_info_override extends KAGWindow_patch_fix_scflags_bookmark_info_original
{
	function KAGWindow_patch_fix_scflags_bookmark_info_override()
	{
		super.KAGWindow(...);
	}

	function KAGWindow()
	{
		KAGWindow_patch_fix_scflags_bookmark_info_override(...);
	}

	var patch_fix_scflags_bookmark_info_initialized = false;
	var patch_fix_scflags_bookmark_info_regex = /(\d*)\.(bmp|ksd)$/g;

	function setSystemStateFromSystemVariables()
	{
		var ret = super.setSystemStateFromSystemVariables(...);
		if (!this.patch_fix_scflags_bookmark_info_initialized)
		{
			this.patch_fix_scflags_bookmark_info_initialized = true;
			if (typeof(Storages.dirlist) === "Object")
			{
				var storages = Storages.dirlist(saveDataLocation + "/");
				for (var i = 0; i < storages.count; i += 1)
				{
					var file = storages[i];
					if (file.substring(0, dataName.length) === dataName)
					{
						var matches = this.patch_fix_scflags_bookmark_info_regex.match(file);
						if (matches.count > 0)
						{
							var num = matches[1] | 0;
							if (this.bookMarkDates[num] == "" || this.bookMarkNames[num] == "")
							{
								try
								{
									var modestr = "";
									if (saveThumbnail)
									{
										modestr += "o" + calcThumbnailSize().size;
									}
									var data = global.safeEvalStorage(saveDataLocation + "/" + file, modestr);
									this.bookMarkDates[num] = data.core.storeTime;
									this.bookMarkNames[num] = data.core.currentPageName;
								}
								catch (e) {}
							}
						}
					}
				}
			}
		}
		return ret;
	}

}
global.KAGWindow = global.KAGWindow_patch_fix_scflags_bookmark_info_override;
