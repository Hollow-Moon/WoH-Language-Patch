
global.KAGWindow_patch_discord_rich_presence_original = global.KAGWindow;
class KAGWindow_patch_discord_rich_presence_override extends global.KAGWindow_patch_discord_rich_presence_original
{
	var discord_start_timestamp = 0;
	var discord_inited = false;
	function KAGWindow_patch_discord_rich_presence_override()
	{
		super.KAGWindow(...);

		if (this.isMain && !this.discord_inited)
		{
			// need seconds here
			this.discord_start_timestamp = (new global.Date()).getTime() \ 1000;
			var application_id = "";
@if(GAME_FATE)
			application_id = "918174971111542834";
@endif
@if(GAME_FHAT)
			application_id = "918175439418163230";
@endif
@if(GAME_WOHN)
			application_id = "918175639801065494";
@endif
			if (application_id !== "")
			{
				global.DiscordRPC.Initialize(application_id, void, false, "");
				var dic = %[];
				dic.largeImageKey = "icon";
				dic.state = "Just initialized";
				dic.startTimestamp = this.discord_start_timestamp;
				global.DiscordRPC.UpdatePresence(dic);
				this.discord_inited = true;
			}
		}
	}

	function KAGWindow()
	{
		this.KAGWindow_patch_discord_rich_presence_override(...);
	}

	function finalize()
	{
		if (this.isMain && this.discord_inited)
		{
			global.DiscordRPC.ClearPresence();
			global.DiscordRPC.Shutdown();
			this.discord_inited = false;
		}
		return super.finalize(...);
	}

	var last_discord_updated_text = "";
	var last_discord_updated_label = "";

	function onConductorLabel(label, page)
	{
		var ret = super.onConductorLabel(...);
		if (this.isMain && this.discord_inited)
		{
			var new_page_name = this.currentPageName;
			var new_label_text = "";
			var on_title_menu = false;
@if(GAME_FATE||GAME_FHAT)
			if (typeof(global.titlemenu_object) === "Object" && typeof(global.titlemenu_object.menulayer) === "Object" && global.titlemenu_object.menulayer.visible)
			{
				on_title_menu = true;
			}
			else if (typeof(global.titlemenu) === "Object")
			{
				on_title_menu = true;
			}
			else if (typeof(global.ticket_menu) === "Object")
			{
				on_title_menu = true;
			}
@endif
@if(GAME_WOHN)
			if (typeof(this.tflags) === "Object" && this.tflags.tt_opened)
			{
				on_title_menu = true;
			}
@endif
			if (typeof(this.currentLabel) === "String" && this.currentLabel === "*title")
			{
				on_title_menu = true;
			}
			if (on_title_menu)
			{
				new_page_name = "Title Menu";
			}
			if (!on_title_menu && typeof(this.currentLabel) === "String" && this.currentLabel.substr(0, 5) === "*page")
			{
				new_label_text = "Page " + this.currentLabel.substr(5);
			}
			if (this.last_discord_updated_text !== new_page_name || this.last_discord_updated_label !== new_label_text)
			{
				var dic = %[];
				dic.largeImageKey = "icon";
				dic.state = global.__(new_page_name);
				if (new_label_text !== "")
				{
					dic.details = new_label_text;
				}
				dic.startTimestamp = this.discord_start_timestamp;
				global.DiscordRPC.UpdatePresence(dic);
				this.last_discord_updated_text = new_page_name;
				this.last_discord_updated_label = new_label_text;
			}
		}
		return ret;
	}
}
global.KAGWindow = global.KAGWindow_patch_discord_rich_presence_override;
