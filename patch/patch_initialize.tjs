
global.initialize_patch_entered = false;

global.dm_old = global.Debug.message;

delete global.dm;
global.dm = function(){};

global.System.createAppLock_old = global.System.createAppLock;
global.System.createAppLock = function()
{
	return true;
};

global.Storages.addAutoPath_old = global.Storages.addAutoPath;
global.Storages.addAutoPath = function(){};

global.System.getArgument_old = global.System.getArgument;
global.System.getArgument = function(arg)
{
	if (arg === "-ovr")
	{
		return void;
	}
	return global.System.getArgument_old(...);
};

delete global.KAGLoadScript;
global.KAGLoadScript = function(name)
{
	if (global.initialize_patch_entered == false)
	{
		return;
	}
	global.dm("Loading " + name);
	var result;
	var start = global.System.getTickCount();
	try
	{
		result = global.Scripts.execStorage(name);
	}
	catch (e)
	{
		var end = global.System.getTickCount();
		global.dm(name + " failed to load in " + (end - start) + "ms");
		var exception_message = "Unknown error";
		if (typeof(e) === "Object" && isvalid(e))
		{
			if (typeof(e.message) === "String")
			{
				exception_message = e.message;
			}
			global.dm("Exception: " + exception_message);
		}
		var trace_string = global.Scripts.getTraceString();
		if (trace_string !== "")
		{
			global.dm("Trace: " + trace_string);
		}
		throw e;
	}
	var end = global.System.getTickCount();
	global.dm(name + " was loaded in " + (end - start) + "ms");
	return result;
};

global.RootDirectory = global.Storages.getFullPath(".") + "/";

// Stub KAGWindow
class KAGWindow
{
	var flags = %[];
	var sflags = %[];
	var tflags = %[];
	var scflags = %[];
	var isHd = false;
	var visible = false;

	function KAGWindow() {
		global.KAGWindowStub = global.KAGWindow;
		global.KAGWindow = null;
		delete global.KAGWindow;
	}

	function process(name)
	{
		(global.initialize_patch incontextof global)();
	}
}

{
	if (typeof(global.kag) === "Object")
	{
		delete global.kag;
	}
}

if (typeof(global.initialize_patch) === "Object")
{
	global.Debug.message("global.initialize_patch already exists; not setting it.");
	return;
}
else
{
	global.Debug.message("global.initialize_patch does not exist.");
}

global.initialize_patch = function()
{
	global.Debug.message("Reached top of initialize_patch.");

	global.kag = void;
	delete global.kag;
	global.initialize_patch_entered = true;
	global.dm = global.dm_old;
	global.System.createAppLock = global.System.createAppLock_old;
	global.Storages.addAutoPath = global.Storages.addAutoPath_old;
	global.System.getArgument = global.System.getArgument_old;

	if (typeof(global.initialize_patch_override) === "Object")
	{
		return global.initialize_patch_override();
	}

	global.dm("Loading patch_loadlist.arr...");

	var start = global.System.getTickCount();

	var to_load = global.Scripts.evalStorage("patch_loadlist.arr");

	global.dm("Loaded patch_loadlist.arr in " + (global.System.getTickCount() - start) + "ms.");

	start = global.System.getTickCount();

	var current_category = "";

	for (var i = 0, internal_forloop_count = to_load.count; i < internal_forloop_count; i += 1)
	{
		var load = to_load[i];
		var filename = "";
		var loadastype = "";
		var tjs_expression = "";
		var optional = 0;
		var preprocessor_expression = "";
		var category = "";
		if (typeof(load) === "String")
		{
			filename = load;
		}
		else if (typeof(load) === "Object")
		{
			filename += load[0];
			loadastype += load[1];
			if (typeof(load[2]) === "Object" && load[2] instanceof "Array")
			{
				var tjs_expression_array = [];
				for (var j = 0, internal_forloop_count = load[2].count; j < internal_forloop_count; j += 1)
				{
					var item = load[2][j];
					tjs_expression_array.add(("typeof(global.%s) %s \"%s\"").sprintf(item[0], typeof(item[2]) === "String" ? item[2] : "!==", typeof(item[1]) === "String" ? item[1] : "Object"));
				}
				tjs_expression += tjs_expression_array.join("&&");
			}
			else
			{
				tjs_expression += load[2];
			}
			optional |= load[3];
			preprocessor_expression += load[4];
			category += load[5];
		}
		if (category !== "")
		{
			if (current_category !== "")
			{
				global.dm("Category " + current_category + " finished in " + (global.System.getTickCount() - start) + "ms.");
			}
			current_category = category;
			global.dm("Category " + current_category + " loading...");
			start = global.System.getTickCount();
			continue;
		}
		if (loadastype === "")
		{
			loadastype = global.Storages.extractStorageExt(filename);
		}
		if (optional !== 0)
		{
			if (!global.Storages.isExistentStorage(filename))
			{
				continue;
			}
		}
		if (tjs_expression !== "")
		{
			if (!global.Scripts.eval(tjs_expression))
			{
				continue;
			}
		}
		if (preprocessor_expression !== "")
		{
			if (!global.Scripts.eval(("@if(%s)\ntrue\n@endif\n").sprintf(preprocessor_expression)))
			{
				continue;
			}
		}
		if (filename !== "")
		{
			if (filename.charAt(filename.length - 1) === ";")
			{
				try
				{
					global.Scripts.exec(filename);
					global.dm("The following executed: " + filename);
				}
				catch (e)
				{
					global.dm("The following failed to execute: " + filename);
					var exception_message = "Unknown error";
					if (typeof(e) === "Object" && isvalid(e))
					{
						if (typeof(e.message) === "String")
						{
							exception_message = e.message;
						}
						global.dm("Exception: " + exception_message);
					}
					var trace_string = global.Scripts.getTraceString();
					if (trace_string !== "")
					{
						global.dm("Trace: " + trace_string);
					}
					throw e;
				}
			}
			else
			{
				var start = global.System.getTickCount();
				global.dm("Loading " + filename);
				try
				{
					if (loadastype === ".ks")
					{
						var old_level = global.kag.conductor.debugLevel;
						global.kag.conductor.debugLevel = global.tkdlNone;
						global.kag.process(filename, "", false, true, true);
						global.kag.conductor.debugLevel = old_level;
					}
					else if (loadastype === ".dll")
					{
						global.Plugins.link(filename);
					}
					else if (loadastype === ".tjs")
					{
						global.Scripts.execStorage(filename);
					}
				}
				catch (e)
				{
					var end = global.System.getTickCount();
					global.dm(filename + " failed to load in " + (end - start) + "ms");
					var exception_message = "Unknown error";
					if (typeof(e) === "Object" && isvalid(e))
					{
						if (typeof(e.message) === "String")
						{
							exception_message = e.message;
						}
						global.dm("Exception: " + exception_message);
					}
					var trace_string = global.Scripts.getTraceString();
					if (trace_string !== "")
					{
						global.dm("Trace: " + trace_string);
					}
					global.dm("Full path: " + global.Storages.getPlacedPath(filename));
					throw e;
				}
				var end = global.System.getTickCount();
				global.dm(filename + " was loaded in " + (end - start) + "ms");
			}
		}

	}

	/*
		コマンドラインパラメータとして -ovr が指定されていれば
		そのパラメータを TJS 式として実行
	*/
	{
		var ovr = global.System.getArgument("-ovr");
		if (ovr !== void && ovr !== "yes")
		{
			global.Scripts.eval(ovr);
		}
	}

	if (global.System.getArgument('-testexecuteonly') === "true")
	{
		global.System.terminate(0);
	}
	else
	{
		global.kag.process("first.ks", '', true, true);
		if (!global.kag.fullScreen)
		{
			global.kag.visible = true;
		}
	}

	global.dm("Reached end of initialize_patch.");
};
