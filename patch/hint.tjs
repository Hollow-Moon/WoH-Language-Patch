
global.Window_hint_original = Window;
class Window_hint_override extends Window_hint_original
{
	function Window_hint_override()
	{
		super.Window(...);
	}

	function Window()
	{
		Window_hint_override(...);
	}

	var hintlayer = void;

	function initHintLayer()
	{
		hintlayer = new Layer(this, primaryLayer);
		{
			var l = hintlayer;
			l.visible = false;
			l.ignoreHintSensing = true;
			l.hitThreshold = 256;
			if (["Integer", "Real"].find(typeof(this.innerHeightScale)) !== -1) {
				l.font.height = Math.ceil(9 / Math.min(1, innerHeightScale)); // upscale font when using window size < x1.0
			} else {
				l.font.height = 9;
			}
			l.font.face = __s("MS UI Gothic", "hint_font_face");
		}

		// hintDelay = 500; // default
		// hintDelay = 0; // immediate
		// hintDelay = -1; // never
		// hintDelay = 1000; // slow
	}

	var hintHorizontalPadding = 4;
	var hintVerticalPadding = 6;
	var hintLineSpacing = 6;
	var tooltipBorderColor = 0xffffe1;
	var mouseCursorSize = 20;

	function onHintChanged(text, x, y, isshow)
	{
		if (hintlayer === void) {
			initHintLayer();
		}
		if (isshow) {
			var lines = text.split("\n");
			var longestLineIndex = 0;
			for (var i = 1, internal_forloop_count = lines.count; i < internal_forloop_count; i += 1) {
				if (lines[i].length > lines[longestLineIndex].length) {
					longestLineIndex = i;
				}
			}

			var fontHeight = hintlayer.font.height;
			var w = hintlayer.font.getTextWidth(lines[longestLineIndex]) + hintHorizontalPadding * 2;
			var h = fontHeight * lines.count + hintLineSpacing * (lines.count-1) + hintVerticalPadding * 2;
			hintlayer.setImageSize(w, h);
			hintlayer.setSizeToImageSize();

@if(HD_MODE)
			if (typeof this.fullScreened === "Integer" && fullScreened) {
				var windowToScreenRatio = (innerHeight*scaleFactor) / System.screenHeight;
				x -= (System.screenWidth - (innerWidth*global.get_wideScaleFactor() / windowToScreenRatio)) / 2;
				x *= windowToScreenRatio;
				y *= windowToScreenRatio;
			}

			x /= global.get_wideScaleFactor();
			y /= scaleFactor;
@endif
@if(ZOOM_WINDOW)
			if (["Integer", "Real"].find(typeof(this.innerHeightScale)) !== -1)
			{
				x /= innerWidthScale;
				y /= innerHeightScale;
				if ((x+w) > innerWidth/innerWidthScale) { x = innerWidth/innerWidthScale - w; }
				if ((y+mouseCursorSize+h) > innerHeight/innerHeightScale) { y = innerHeight/innerHeightScale - h - mouseCursorSize/innerHeightScale; }
				hintlayer.setPos(x, y + mouseCursorSize/innerHeightScale);
			}
			else
@endif
			{
				if ((x+w) > innerWidth) { x = innerWidth - w; }
				if ((y+mouseCursorSize+h) > innerHeight) { y = innerHeight - h - mouseCursorSize; }
				hintlayer.setPos(x, y + mouseCursorSize);
			}

			hintlayer.fillRect(0, 0, w, h, 0);
			hintlayer.colorRect(0, 0, w, h, clInfoBk, 196);

			hintlayer.colorRect(0,   0,   1, h, tooltipBorderColor);
			hintlayer.colorRect(0,   0,   w, 1, tooltipBorderColor);
			hintlayer.colorRect(w-1, 0,   1, h, tooltipBorderColor);
			hintlayer.colorRect(0,   h-1, w, 1, tooltipBorderColor);

			var lineY = hintVerticalPadding;
			for (var i = 0, internal_forloop_count = lines.count; i < internal_forloop_count; i += 1) {
				hintlayer.drawText(hintHorizontalPadding, lineY, lines[i], clInfoText, 220);
				lineY += fontHeight + hintLineSpacing;
			}
			hintlayer.visible = true;
			hintlayer.bringToFront();
		} else {
			hintlayer.visible = false;
		}
	}
}
global.Window = Window_hint_override;
