@if(GAME_FATE||GAME_FHAT)
global.originalResWidth = 800;
global.originalResHeight = 600;
@endif
@if(GAME_WOHN)
global.originalResWidth = 1024;
global.originalResHeight = 576;
@endif

global.isCropped = false;
global.scaleFactor = 1;
global.wideFactor = 1;
global.cropFactor = 1;
@if(GAME_FATE||GAME_FHAT)
global.cubicRatio = 4/3;
@endif
@if(GAME_WOHN)
global.cubicRatio = 16/9;
@endif
global.standardWideRatio = 16/9;

@if(GAME_FATE)
global.hdPatchesNames = [
	devMode? "ImagesHD" : "HD",
	devMode?  "ImagesHD2" : "FHD",
	devMode?  "ImagesHD3" : "UHD"
];
global.scaleFactors = [1.6, 2.4, 4];
global.hdNames = ["hd", "fhd", "uhd"];
@endif
@if(GAME_FHAT)
global.hdPatchesNames = [
	devMode? "ImageHD" : "HD",
	devMode?  "ImageHD2" : "FHD",
];
global.scaleFactors = [2, 4];
global.hdNames = ["hd", "fhd"];
@endif
@if(GAME_WOHN)
global.hdPatchesNames = [
	devMode? "ImageHD" : "HD",
	devMode?  "ImageHD2" : "FHD",
];
global.scaleFactors = [2, 4];
global.hdNames = ["hd", "fhd"];
@endif

global.scaleFactorsToPatchName = %[];
global.scaleFactorsToHdNames = %[];
for (var i=0; i<global.scaleFactors.count; i++) {
	global.scaleFactorsToPatchName[global.scaleFactors[i]] = global.hdPatchesNames[i];
	global.scaleFactorsToHdNames[global.scaleFactors[i]] = global.hdNames[i];
}

property wideRulePatchName { getter() { return devMode? "RuleWide" : mainPatchName; } }
global.wideRuleFolderName = devMode? "" : "RuleWide";

global.hdScaleFactorFlagName = "hdScaleFactor";
global.wideScreenFlagName = "wideScreen";
global.autoWideRatioFlagName = "autoWideRatio";
global.wideRatioFlagName = "wideRatio";

global.devHdImageFolders = ["BGImageHD", "FGImageHD", "ImageHD", "ImagesClassicHD", "ImagesConfigHD", "ImagesMobile", "ImagesPatchHD", "ImagesPS2HD", "ImagesTitleHD", "ImagesTrialHD", "ImagesHanafudaHD", "RuleHD", "Image_transparentHDcolor"];

property systemRatio { getter { return System.screenWidth / System.screenHeight; } }

property inverseScaleFactor { getter() { return 1/scaleFactor; } }
property inverseWideFactor { getter() { return 1/wideFactor; } }
property inverseCubicRatio { getter() { return 1/cubicRatio; } }
property wideScaleFactor { getter() { return scaleFactor * wideFactor; } }

property hdPatchName { getter() { return global.scaleFactorsToPatchName[scaleFactor]; } }
property hdFolderName {getter() { return global.scaleFactorsToHdNames[scaleFactor]; }}
property hdFolderSuffix {getter() { return "-" + hdFolderName; }}
property hdPatchSuffix {getter() { return "_" + hdFolderName; }}

function addHdPaths(patches_arr, patchName, folder="", innerPath="") {
	var separator = folder === ""? "" : "-";
	for (var i = 0; i < hdNames.count; i++) {
		patches_arr.add([patchName, folder + separator + hdNames[i] + innerPath]);
		if (hdNames[i] == hdFolderName) {
			break;
		}
	}
	var patchNameHd = patchName === mainPatchName? hdPatchName : (patchName + hdPatchSuffix);
	if (folder != "") {
		patches_arr.add([patchNameHd, folder + innerPath]);
	}
	for (var i = 0; i < hdNames.count; i++) {
		patches_arr.add([patchNameHd, folder + separator + hdNames[i] + innerPath]);
		if (hdNames[i] == hdFolderName) {
			break;
		}
	}
}

function unloadAllHdPatches(kag=global.kag) {
	var originalScaleFactor = scaleFactor;

	for (var i = 0; i < scaleFactors.count; i++) {
		scaleFactor = scaleFactors[i];
		unloadHdPatch(kag);
	}
	scaleFactor = originalScaleFactor;
}

function loadHdPatch(kag=global.kag)
{
	if (hdPatchName === void) {
		return;
	}
	var patches_arr = [];
	patches_arr.add([hdPatchName]);
	if (devMode && scaleFactor === global.scaleFactors[0]) {
		for (var i = 0, internal_forloop_count = devHdImageFolders.count; i < internal_forloop_count; i += 1) {
			patches_arr.add([devHdImageFolders[i]]);
		}
	}
	addHdPaths(patches_arr, opPatchName) if get_opPatchExists();
	if (typeof(kag) === "Object")
	{
		addHdPaths(patches_arr, wideRulePatchName, wideRuleFolderName) if kag.scflags[wideScreenFlagName];

		addHdPaths(patches_arr, maturePatchName, uncensoredMatureFolderName) if kag.sflags[matureFlagName];
		addHdPaths(patches_arr, hPatchName, uncensoredHFolderName) if kag.sflags[hFlagName];

		addHdPaths(patches_arr, languagePatchName(japaneseLanguage), imagesFolderName);
		addHdPaths(patches_arr, languagePatchName(kag.sflags.language), imagesFolderName) if kag.sflags.language !== void && kag.sflags.language !== japaneseLanguage;
	}

	load_patches(patches_arr);
	invalidate patches_arr;
	delete patches_arr;

	if (typeof(kag) === "Object")
	{
		updateDecensorConfig(kag);
	}
}

function unloadHdPatch(kag=global.kag)
{
	if (hdPatchName === void) {
		return;
	}
	var patches_arr = [];
	patches_arr.add([hdPatchName]);
	if (devMode && scaleFactor === global.scaleFactors[0]) {
		for (var i = 0, internal_forloop_count = devHdImageFolders.count; i < internal_forloop_count; i += 1) {
			patches_arr.add([devHdImageFolders[i]]);
		}
	}
	addHdPaths(patches_arr, opPatchName) if get_opPatchExists();
	if (typeof(kag) === "Object")
	{
		addHdPaths(patches_arr, wideRulePatchName, wideRuleFolderName) if kag.scflags[wideScreenFlagName];

		addHdPaths(patches_arr, maturePatchName, uncensoredMatureFolderName) if kag.sflags[matureFlagName];
		addHdPaths(patches_arr, hPatchName, uncensoredHFolderName) if kag.sflags[hFlagName];
		
		addHdPaths(patches_arr, languagePatchName(japaneseLanguage), imagesFolderName);
		addHdPaths(patches_arr, languagePatchName(kag.sflags.language), imagesFolderName) if kag.sflags.language !== void && kag.sflags.language !== japaneseLanguage;
	}

	unload_patches(patches_arr);
	invalidate patches_arr;
	delete patches_arr;

	if (typeof(kag) === "Object")
	{
		updateDecensorConfig(kag);
	}
}

function initializeQuality(scale) {
	unloadHdPatch(this);

	scaleFactor = scale;
	hdScaleFactor = scale;

	loadHdPatch(this);
}

function initializeWide(ratio) {
	wideScreen = ratio > cubicRatio;
	wideRatio = ratio;
@if(SCALE_USING_FLOATING_POINT)
	wideFactor = originalResHeight * ratio / originalResWidth;
@endif
@if(!SCALE_USING_FLOATING_POINT)
	wideFactor = originalResHeight * ratio \ originalResWidth;
	if (!wideFactor)
	{
		wideFactor = 1;
	}
@endif

	if (wideScreen) {
		var patches_arr = [[wideRulePatchName, wideRuleFolderName]];
@if(HD_MODE)
		addHdPaths(patches_arr, wideRulePatchName, wideRuleFolderName) if this.isHd;
@endif
		load_patches(patches_arr);
	} else {
		var patches_arr = [[wideRulePatchName, wideRuleFolderName]];
@if(HD_MODE)
		addHdPaths(patches_arr, wideRulePatchName, wideRuleFolderName) if this.isHd;
@endif
		unload_patches(patches_arr);
	}
}

function onQualityMenuItemClick(sender) {
	with (sender.owner) {
		var changed = false;
		if (sender.scaleFactor !== .hdScaleFactor) {
@if(!KIRIKIROID&&!KIRIKIRISDL2)
			{
				global.full_game_reload("-hdscalefactor="+sender.scaleFactor);
			}
@endif
			.extraConductor.clear();
			.lineBreak.loadedImage = null;
			.pageBreak.loadedImage = null;
			.reload_scenario_prepare();
			(initializeQuality incontextof sender.owner)(sender.scaleFactor);
			changed = true;
			sender.checked = true;
		}
		if (changed) {
			changeResolution(sender.owner);
			.reload_scenario_execute();
		}
	}
}

function onWideMenuItemClick(sender, ratio, cropped=false) {
	if (ratio == "standard") {
		ratio = cubicRatio;
	} else if (ratio == "wide") {
		ratio = systemRatio;
	}
	
	if (ratio !== void && !floatEquals(ratio, sender.owner.wideRatio))
	{
@if(!KIRIKIROID&&!KIRIKIRISDL2)
		{
			global.full_game_reload("-wideratio=" + global.real_to_hex_string(ratio));
		}
@endif
		with (sender.owner) {
			.extraConductor.clear();
			.reload_scenario_prepare();
			(initializeWide incontextof sender.owner)(ratio);
			changeResolution(sender.owner);
			.reload_scenario_execute();
		}
	}
	if (cropped != isCropped)
	{
		with (sender.owner) {
@if(SCALE_USING_FLOATING_POINT)
			if (cropped)
			{
				isCropped = true;
				cropFactor = 1 / cubicRatio;
			}
			else
@endif
			{
				isCropped = false;
				cropFactor = 1;
			}

			.extraConductor.clear();
			.reload_scenario_prepare();

			.setSize(.width, .height);

			.reload_scenario_execute();
		}
	}

	sender.checked = true;
}

function changeResolution(win) {
	with (win) {
		(updateResolution incontextof win)(%[]);
		if (typeof(._primaryLayer) === "Object")
		{
			._primaryLayer.update_bounds_hd_layer();
		}
		if (typeof(.hintlayer) === "Object")
		{
			invalidate .hintlayer;
			.hintlayer = void;
		}
		if (typeof(global.flow_tracker_object) === "Object" && typeof(global.flow_tracker_object.isTest) === "Integer" && global.flow_tracker_object.isTest)
		{
			global.flow_tracker_object.isTest = false;
			global.flow_tracker_object.isTest = true;
		}
@if(!ZOOM_WINDOW)
		.setSize(originalResWidth, originalResHeight);
		.setInnerSize(originalResWidth, originalResHeight);
@endif
	}
}

function updateResolution(mp)
{
	isPossibleChangeScreen	= true if mp.changescreen === true;
	var curfullscreen	= false;
	curfullscreen <-> fullScreen;	//	フルスクリーン状態を解除
	//pxWidth = exWidth = size[0];
	//pxHeight = exHeight = size[1];
@if(ZOOM_WINDOW)
	setInnerSize(innerWidth, innerHeight);	//	ウィンドウのクライアントサイズを変更
@endif
@if(!ZOOM_WINDOW)
	setSize(originalResWidth, originalResHeight);
	setInnerSize(originalResWidth, originalResHeight);
	setZoom(1, 1);
@endif

@if(ZOOM_WINDOW)
	addHook("afterChangeScreenMode", fullScreenAfterChangeScreenMode) if curfullscreen;
@endif
@if(!ZOOM_WINDOW)
	curfullscreen <-> fullScreen;	//	フルスクリーン状態を復帰
@endif

	isPossibleChangeScreen	= false if mp.changescreen === false;
}


function initializeHDPatch()
{
@if(HD_MODE)
	if (System.getArgument("-hdscalefactor") !== void)
	{
		hdScaleFactor = +System.getArgument("-hdscalefactor");
	}
	unloadAllHdPatches(this);
	(initializeQuality incontextof this)(hdScaleFactor);
	if (System.getArgument("-wideratio") !== void)
	{
		(initializeWide incontextof this)(System.getArgument("-wideratio") * 1.0);
	}
	else
	{
		(initializeWide incontextof this)(wideRatio);
	}
	changeResolution(this);
@endif
@if(!HD_MODE)
	unloadAllHdPatches(this);
@endif
}
