
{
	Storages.isExistentStorage_patch_storages_original = Storages.isExistentStorage;
	Storages.isExistentStorage = function(storage)
	{
		if ((storage === void) || (storage == null))
		{
			return false;
		}
@if(kirikiriz)
		if (storage.trim() === "")
		{
			return false;
		}
@endif

		return isExistentStorage_patch_storages_original(...);
	} incontextof Storages;
}

{
	// TODO: clean out the auto path table
	var paths_to_add = [];
	var paths_to_remove = [];
	var paths = [
		"voice",
		"video",
		"others",
		"etc",
		"rule",
		"sound",
		"bgm",
		"fg",
		"fgimage",
		"bg",
		"bgimage",
		"event",
		"scenario",
		"image",
		"system",
		"plugin",
		"k2compat",
		"override",
		"update",
		"patch"
	];
	var arcdelim = System.getArgument("-arcdelim");
	if (!arcdelim)
	{
		arcdelim = ">";
	}
	if ((typeof(Storages.dirlist) !== "Object") || (typeof(Storages.isExistentDirectory) !== "Object"))
	{
		if (Storages.isExistentStorage("fstat.dll"))
		{
			Plugins.link("fstat.dll");
		}
	}

	for (var i = 2; i < 50; i += 1)
	{
		var path = "patch" + i;
		var paths_to_search = [
			System.exePath + path + ".xp3",
			System.exePath + "data/" + path,
			System.exePath + path,
			System.exePath + "patch/" + path,
			System.exePath + "content-data/" + path,
		];
		var found = false;
		for (var j = 0; j < paths_to_search.count && typeof(paths_to_search[j]) === "String"; j += 1)
		{
			var path = paths_to_search[j];
			{
@if(!KIRIKIROID)
				if (Storages.isExistentDirectory(path + "/"))
				{
					found = true;
					break;
				}
@endif
				if (Storages.isExistentStorage(path))
				{
					found = true;
					break;
				}
			}
		}
		if (found)
		{
			paths.add(path);
		}
		else
		{
			break;
		}
	}
	var paths_to_search = [
		"%sdata.xp3" + arcdelim + "%s",
		"%s%s.xp3",
		"%sdata/%s",
		"%s%s",
		"%spatch/%s",
		"%scontent-data/%s",
	];

	for (var i = 0; i < paths_to_search.count && typeof(paths_to_search[i]) === "String"; i += 1)
	{
		for (var j = 0; j < paths.count; j += 1)
		{
			var path = paths_to_search[i].sprintf(System.exePath, paths[j]);
			var spos = path.indexOf(arcdelim);
			paths_to_remove.add(path + "/");
			paths_to_remove.add(path + arcdelim);
			if (spos !== -1)
			{
				if (Storages.isExistentStorage(path.substring(0, spos)))
				{
					paths_to_add.add(path + "/");
				}
			}
			else
			{
@if(!KIRIKIROID)
				if (Storages.isExistentDirectory(path + "/"))
				{
					paths_to_add.add(path + "/");
				}
@endif
				if (Storages.isExistentStorage(path))
				{
					paths_to_add.add(path + arcdelim);
				}
			}
		}
	}

	if (devMode)
	{
		var devpaths = [
			"../common/patch/k2compat",
			"../common/plugin",
			"../common/pluginpatch",
			"../common/imageshistory",
			"patch",
			"etc",
			"others",
			"voice",
			"rule",
			"sound",
			"se",
			"bgm",
			"fgimage",
			"bgimage",
			"scenario",
			"image",
			"video",
			"system",
			"plugin",
			"pluginpatch",
			"op",
			"ImagesPatch",
			"ImagesConfig",
			"SEPatch",
@if(GAME_FHAT)
			"voicehanafuda",
			"voicehanafuda2",
			"imagesvita",
			"imageshanafuda",
			"bgm_patch",
			"bgm_vita",
@endif
@if(GAME_FATE)
			"bgm_patch",
			"ImagesClassic",
			"ImagesPS2",
			"ImagesTitle",
			"ImagesSD",
			"ImagesScene",
			"ImagesTrial",
			"BGM_Classic",
			"BGM_Remixed",
			"BGM_PS2",
			"Voice_patch",
			"SoundPatch",
			"Decensor",
@endif
		];
		for (var i = 0; i < devpaths.count && typeof(devpaths[i]) === "String"; i += 1)
		{
			var path = System.exePath + devpaths[i] + "/";
			paths_to_remove.add(path);
@if(!KIRIKIROID)
			if (Storages.isExistentDirectory(path))
			{
				paths_to_add.add(path);
			}
@endif
		}
	}

@if(!DEBUG)
	var dir = System.exePath;
	if (Storages.isExistentDirectory(dir))
	{
		var files = Storages.dirlist(dir);
		var patches = [];
		for (var i = 0; i < files.count; i += 1)
		{
			var fn = files[i];
			if (fn.substr(0, 6) == "patch_" && Storages.extractStorageExt(fn) == ".xp3" && Storages.isExistentStorage(dir + fn))
			{
				patches.add(fn);
			}
		}
		patches.sort("a");
		for (var i = 0; i < patches.count; i += 1)
		{
			var path = dir + patches[i] + arcdelim;
			paths_to_remove.add(path);
			paths_to_add.add(path);
		}
	}
@endif
	for (var i = 0; i < paths_to_remove.count; i += 1)
	{
		Storages.removeAutoPath(paths_to_remove[i]);
	}

	for (var i = 0; i < paths_to_add.count; i += 1)
	{
		Storages.addAutoPath(paths_to_add[i]);
		Debug.message("Adding to auto path: " + paths_to_add[i]);
	}
}
