
global.KAGWindow_patch_store_bookmarkdate_as_epoch_original = global.KAGWindow;
class KAGWindow_patch_store_bookmarkdate_as_epoch_override extends global.KAGWindow_patch_store_bookmarkdate_as_epoch_original
{
	function KAGWindow_patch_store_bookmarkdate_as_epoch_override()
	{
		super.KAGWindow(...);
	}

	function KAGWindow()
	{
		this.KAGWindow_patch_store_bookmarkdate_as_epoch_override(...);
	}

	var bookMarkEpochDates = [];

	function getFormatDate(datetime)
	{
		var date = new global.Date();
		date.setTime(datetime);
		date = global.strftime(global.__("%Y/%m/%d %H:%M:%S"), date);
		return date;
	}

	function getBookMarkDate(num, formated=true)
	{
		var date = this.bookMarkEpochDates[num];
		if (formated)
		{
			if (date == void)
			{
				date = this.bookMarkDates[num];
			}
			if (date != void)
			{
				return this.getFormatDate(date);
			}
			else
			{
				return void;
			}
		}
		return date;
	}

	function getBookMarkInfoFromData(dic, num)
	{
		if (num < this.numBookMarks)
		{
			this.bookMarkEpochDates[num] = dic.storeTime;
		}
		return super.getBookMarkInfoFromData(...);
	}

	function setSystemStateFromSystemVariables()
	{
		var ret = super.setSystemStateFromSystemVariables(...);
		if (this.scflags.bookMarkEpochDates !== void)
		{
			this.bookMarkEpochDates = this.scflags.bookMarkEpochDates;
		}
		return ret;
	}

	function saveSystemVariables()
	{
		if (this.isMain)
		{
			this.scflags.bookMarkEpochDates = this.bookMarkEpochDates;
		}
		return super.saveSystemVariables(...);
	}
}
global.KAGWindow = global.KAGWindow_patch_store_bookmarkdate_as_epoch_override;
