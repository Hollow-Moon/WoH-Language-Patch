
global.KAGWindow_patch_store_bookmarkdate_as_epoch_original = global.KAGWindow;
class KAGWindow_patch_store_bookmarkdate_as_epoch_override extends global.KAGWindow_patch_store_bookmarkdate_as_epoch_original
{
	function KAGWindow_patch_store_bookmarkdate_as_epoch_override()
	{
		super.KAGWindow(...);
	}

	function KAGWindow()
	{
		this.KAGWindow_patch_store_bookmarkdate_as_epoch_override(...);
	}

	var bookMarkEpochDates = [];

	function getFormatDate(datetime)
	{
		var date = global.strftime(global.__("%Y/%m/%d %H:%M:%S"), datetime);
		return date;
	}

	function getBookMarkDate(num, formated=true)
	{
		var epochDate = this.bookMarkEpochDates[num];
		var date;

		if (!formated)
		{
			return epochDate;
		}

		if (epochDate !== void)
		{
			date = new global.Date();
			date.setTime(epochDate);
			return this.getFormatDate(date);
		}

		var legacyDate = this.bookMarkDates[num];
		var re = new global.RegExp("^(\\d{4})/(\\d{2})/(\\d{2}) (\\d{2}):(\\d{2})$");
		var match = re.match(legacyDate);
		if (match.count > 0)
		{
			date = new global.Date(legacyDate);
			return this.getFormatDate(date);
		}
		return legacyDate;
	}

	function getBookMarkInfoFromData(dic, num)
	{
		if (num < this.numBookMarks)
		{
			this.bookMarkEpochDates[num] = dic.storeTime;
		}
		return super.getBookMarkInfoFromData(...);
	}

	function setSystemStateFromSystemVariables()
	{
		var ret = super.setSystemStateFromSystemVariables(...);
		if (this.scflags.bookMarkEpochDates !== void)
		{
			this.bookMarkEpochDates = this.scflags.bookMarkEpochDates;
		}
		return ret;
	}

	function saveSystemVariables()
	{
		if (this.isMain)
		{
			this.scflags.bookMarkEpochDates = this.bookMarkEpochDates;
		}
		return super.saveSystemVariables(...);
	}
}
global.KAGWindow = global.KAGWindow_patch_store_bookmarkdate_as_epoch_override;
