
global.KAGWindow_patch_language_override_original = KAGWindow;
class KAGWindow_patch_language_override_override extends KAGWindow_patch_language_override_original
{
	function KAGWindow_patch_language_override_override()
	{
		super.KAGWindow(...);

		if (this instanceof "KAGWindow" && this.isMain)
		{
			setFontWithLanguage(this.sflags.language, this);
		}

		this.conductor.getPreprocessChCallbacks().add(this.patch_language_override_preprocess_ch);
	}

	function KAGWindow()
	{
		KAGWindow_patch_language_override_override(...);
	}

	function onLanguageMenuItemClick(sender, language)
	{
		if (this.sflags.language == language)
		{
			return;
		}
		saveFontWithLanguage(this);
		unload_language_patch(this.sflags.language, this);
		load_language_patch(language, this);
		this.sflags.language = language;
@if(!GAME_WOHN)
		setFontWithLanguage(this.sflags.language, this);
@endif
		sender.checked = true;
		extraConductor.clear(); // Fix code for AutoPath
@if(GAME_WOHN)
		// Reload the font configuration.
		global.reload_messagelayer_config(this);
		setFontWithLanguage(this.sflags.language, this);
@endif
		onReloadScenarioMenuItemClick(sender);
		if(typeof(global.flow_tracker_object) === "Object" && typeof(flow_tracker_object.flowchart) === "Object" && flow_tracker_object.flowchart.filename !== void)
		{
			flow_tracker_object.isPlayOK();
			currentPageName = f.scripttitle;
		}
	}

	function replace_font_for_special_characters(elm)
	{
		var originalFontFace = current.lineLayer.font.face;
		var special = get_special_characters(this)[originalFontFace];
		if (special !== void && special[0].contains(elm.text))
		{
			var specialCharactersFontFace = special[1];
			tagHandlers.font(%["face" => specialCharactersFontFace]);
			var restoreFontFaceTag = %[tagname:"font", face:"user"];
			conductor.pendings.insert(0, restoreFontFaceTag);
		}
	}

	var replace_character_regex1 = /~/g;
	var replace_character_regex2 = /　/g;
	function replace_characters_regex(text)
	{
		if (
			!global.get_replace_jp_tilde_disabled((typeof(global.kag) === "Object") ? global.kag : this)
			)
		{
			text = text.replace(this.replace_character_regex1, "〜");
		}
		if (
			!global.get_replace_jp_whitespace_disabled((typeof(global.kag) === "Object") ? global.kag : this)
			&& this.sflags.language == englishLanguage
			&& !startsWith("花札", conductor.curStorage) // Not hanafuda
			)
		{
			text = text.replace(this.replace_character_regex2, __s(" ", "replace_ideographic_space"));
		}
		return text;
	}

	function replaceCharacters(obj)
	{
		obj.text = this.replace_characters_regex(obj.text);
	}

	function patch_language_override_preprocess_ch(text, conductor, elm)
	{
		if (typeof(conductor.pendings_prelayout) !== "Object")
		{
			if (global.get_arabic_shaping_enabled((typeof(global.kag) === "Object") ? global.kag : this))
			{
				text = global.shape_arabic(text, true);
			}
		}
		text = this.replace_characters_regex(text);
		return text;
	}
}
global.KAGWindow = KAGWindow_patch_language_override_override;
