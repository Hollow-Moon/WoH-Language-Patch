// Patch names
global.vitaAssetsPatchName = devMode? "ImagesVita" : "vita"; // not the best place to decalre the vita patch name but we need it at this stage
@if(GAME_FATE)
global.hPatchName = "h";
global.hCensorPatchName = mainPatchName;
global.maturePatchName = devMode? "mature" : mainPatchName;
@endif
@if(GAME_FHAT)
global.hPatchName = mainPatchName;
global.hCensorPatchName = vitaAssetsPatchName;
global.maturePatchName = vitaAssetsPatchName;
@endif
global.decensoredPatchName = "decensor";

// Flag names
global.hFlagName = "ghentai";
global.hCensoredFlagName = "censored";
global.matureFlagName = "gmature";
global.decensoredFlagName = "decensor";

function get_hPatchExists() { { return patchExists(hPatchName); } }
function get_isHCensored(kag=global.kag) { { return kag.sflags[hCensoredFlagName] !== void? kag.sflags[hCensoredFlagName] : !kag.sflags[hFlagName]; } } // RN uses hFlagName while HA uses hCensoredFlagName
function get_isMatureCensored(kag=global.kag) { { return !kag.sflags[matureFlagName]; } }
function get_fullyCensored(kag=global.kag) { { return get_isHCensored(kag) && get_isMatureCensored(kag); } }
function get_isDecensored(kag=global.kag) { { return kag.sflags[decensoredFlagName]; } };

// Folder names
global.fullyCensoredFolderName = "censored";

global.censoredHFolderName = "censored_h";
global.censoredMatureFolderName = "censored_mature";

global.uncensoredHFolderName = "h";
global.uncensoredMatureFolderName = "mature";

global.decensoredFolderName = "decensored";

function getTextCensored(text, kag=global.kag) {
	if (text == void) return text;
	if (text.indexOf("|") >=0)
	{
		var splitText = [].split("|", text);
		if (kag.sflags[splitText[2]])
			return splitText[0];
		return splitText[1];
	}
	return text;
}

function isHScene(kag=global.kag)
{
	var name = kag.flags.scriptlabel;
	if (name == void || name == "") return false;

	name = Storages.chopStorageExt(name);
	name = name.replace(/trail_/g,"");
	var split = [].split("-",name);

	return split.length >= 2 && split[1].length == 3;
}

function updateDecensorConfig(kag=global.kag)
{
	var decensor_types = [void, "bishopcruz", "Bellandy", "original"];
	var decensor_config_file_name = "decensor_config.dic";

	var decensor_config;
	var custom_decensor_config_file_path = global.paths_base[0] + decensor_config_file_name;
	if (Storages.isExistentStorage(custom_decensor_config_file_path))
	{
		decensor_config = Scripts.evalStorage(custom_decensor_config_file_path);
	}
	else if (Storages.isExistentStorage(decensor_config_file_name))
	{
		decensor_config = Scripts.evalStorage(decensor_config_file_name);
	}

	if (decensor_config === void) {
		return;
	}

	var decensoredImages = [];
	decensoredImages.assign(decensor_config);

	for (var i = 0, internal_forloop_count = decensoredImages.count; i < internal_forloop_count; i += 2)
	{
		var currentDecensorType = decensor_types[+decensor_config[decensoredImages[i]]];

		for (var j = 1, internal_forloop_count = decensor_types.count; j < internal_forloop_count; j += 1)
		{
			var decensor_type = decensor_types[j];
			var decensor_version_path = decensoredFolderName + "/" + decensoredImages[i] + "/" + decensor_type;
@if(HD_MODE)
			var decensor_version_path_hd = decensoredFolderName + hdFolderSuffix + "/" + decensoredImages[i] + "/" + decensor_type;
@endif
			if(decensor_type == currentDecensorType)
			{
				if(get_isDecensored(kag))
				{
					load_patches([
						[decensoredPatchName, decensor_version_path],
@if(HD_MODE)
						kag.isHd ? [decensoredPatchName, decensor_version_path_hd] : void,
@endif
					]);
					unload_patches([
@if(HD_MODE)
						(!kag.isHd) ? [decensoredPatchName, decensor_version_path_hd] : void,
@endif
					]);
				}
				else
				{
					unload_patches([
						[decensoredPatchName, decensor_version_path],
@if(HD_MODE)
						kag.isHd ? [decensoredPatchName, decensor_version_path_hd] : void,
@endif
					]);
				}
			}
			else
			{
				unload_patches([
					[decensoredPatchName, decensor_version_path],
@if(HD_MODE)
					kag.isHd ? [decensoredPatchName, decensor_version_path_hd] : void,
@endif
				]);
			}
		}
	}
	invalidate decensoredImages;
}
