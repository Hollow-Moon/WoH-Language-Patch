global.VideoOverlay_patch_hd_layer_original = global.VideoOverlay;
class VideoOverlay_patch_hd_layer_override extends global.VideoOverlay_patch_hd_layer_original
{
	function VideoOverlay_patch_hd_layer_override(owner)
	{
		this.patch_hd_layer_owner = owner;
		super.VideoOverlay(...);
	}

	function VideoOverlay()
	{
		this.VideoOverlay_patch_hd_layer_override(...);
	}

	property width
	{
		getter
		{
@if(1)
			if (typeof(this.mp) === "Object")
			{
				return super.width;
			}
@endif
@if(SCALE_USING_FLOATING_POINT)
			return super.width / global.get_wideScaleFactor(this.patch_hd_layer_owner);
@endif
@if(!SCALE_USING_FLOATING_POINT)
			return super.width \ global.get_wideScaleFactor(this.patch_hd_layer_owner);
@endif
		}
	}

	property height
	{
		getter
		{
@if(1)
			if (typeof(this.mp) === "Object")
			{
				return super.height;
			}
@endif
@if(SCALE_USING_FLOATING_POINT)
			return super.height / this.patch_hd_layer_owner.hdScaleFactor;
@endif
@if(!SCALE_USING_FLOATING_POINT)
			return super.height \ this.patch_hd_layer_owner.hdScaleFactor;
@endif
		}
	}

	property originalWidth
	{
		getter
		{
@if(SCALE_USING_FLOATING_POINT)
			return super.originalWidth / global.get_wideScaleFactor(this.patch_hd_layer_owner);
@endif
@if(!SCALE_USING_FLOATING_POINT)
			return super.originalWidth \ global.get_wideScaleFactor(this.patch_hd_layer_owner);
@endif
		}
	}

	property originalHeight
	{
		getter
		{
@if(SCALE_USING_FLOATING_POINT)
			return super.originalHeight / this.patch_hd_layer_owner.hdScaleFactor;
@endif
@if(!SCALE_USING_FLOATING_POINT)
			return super.originalHeight \ this.patch_hd_layer_owner.hdScaleFactor;
@endif
		}
	}

	function setBounds(left, top, width, height)
	{
@if(1)
		if (typeof(this.mp) === "Object")
		{
			return super.setBounds(left, top, width, height);
		}
@endif
		return super.setBounds(left*global.get_wideScaleFactor(this.patch_hd_layer_owner), top*this.patch_hd_layer_owner.hdScaleFactor, width*global.get_wideScaleFactor(this.patch_hd_layer_owner), height*this.patch_hd_layer_owner.hdScaleFactor);
	}
}
global.VideoOverlay = global.VideoOverlay_patch_hd_layer_override;
